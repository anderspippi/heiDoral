fun main() {
    SOLAR_MASS = 4.0 * PI * PI;
    let n = argv(0).toInt32().unwrap();
    let bodies = NBodySystem();

    var i = 0;
    println(bodies.energy().toString());

    while i < n {
        bodies.advance(0.01);
        i = i + 1;
    }

    println(bodies.energy().toString());
}

class NBodySystem {
    let bodies: Array[Body] = Array[Body](5L);

    {
        self.bodies(0) = Body::sun();
        self.bodies(1) = Body::jupiter();
        self.bodies(2) = Body::saturn();
        self.bodies(3) = Body::uranus();
        self.bodies(4) = Body::neptune();

        var px = 0.0;
        var py = 0.0;
        var pz = 0.0;

        var i = 0L;

        while i < self.bodies.length() {
            px = px + self.bodies.get(i.toInt32()).vx * self.bodies.get(i.toInt32()).mass;
            py = py + self.bodies.get(i.toInt32()).vy * self.bodies.get(i.toInt32()).mass;
            pz = pz + self.bodies.get(i.toInt32()).vz * self.bodies.get(i.toInt32()).mass;

            i = i + 1L;
        }

        self.bodies.get(0).offsetMomentum(px, py, pz);
    }

    fun advance(dt: Double) {
        var i = 0L;

        while i < self.bodies.length() {
            let iBody = self.bodies.get(i.toInt32());
            var j = i + 1L;

            while j < self.bodies.length() {
                let dx = iBody.x - self.bodies.get(j.toInt32()).x;
                let dy = iBody.y - self.bodies.get(j.toInt32()).y;
                let dz = iBody.z - self.bodies.get(j.toInt32()).z;

                let dSquared = dx * dx + dy * dy + dz * dz;
                let distance = dSquared.sqrt();
                let mag = dt / (dSquared * distance);

                iBody.vx = iBody.vx - dx * self.bodies.get(j.toInt32()).mass * mag;
                iBody.vy = iBody.vy - dy * self.bodies.get(j.toInt32()).mass * mag;
                iBody.vz = iBody.vz - dz * self.bodies.get(j.toInt32()).mass * mag;

                self.bodies.get(j.toInt32()).vx = self.bodies.get(j.toInt32()).vx + dx * iBody.mass * mag;
                self.bodies.get(j.toInt32()).vy = self.bodies.get(j.toInt32()).vy + dy * iBody.mass * mag;
                self.bodies.get(j.toInt32()).vz = self.bodies.get(j.toInt32()).vz + dz * iBody.mass * mag;

                j = j + 1L;
            }

            i = i + 1L;
        }

        var i = 0L;

        while i < self.bodies.length() {
            let body = self.bodies.get(i.toInt32());

            body.x = body.x + dt * body.vx;
            body.y = body.y + dt * body.vy;
            body.z = body.z + dt * body.vz;

            i = i + 1L;
        }
   }

   fun energy() -> Double {
      var dx = 0.0;
      var dy = 0.0;
      var dz = 0.0;
      var distance = 0.0;
      var e = 0.0;

      var i = 0L;

      while i < self.bodies.length() {
         let iBody = self.bodies.get(i.toInt32());
         e = e + 0.5 * iBody.mass *
            ( iBody.vx * iBody.vx
                + iBody.vy * iBody.vy
                + iBody.vz * iBody.vz );

        var j = i+1L;

        while j < self.bodies.length() {
            let jBody = self.bodies.get(j.toInt32());

            dx = iBody.x - jBody.x;
            dy = iBody.y - jBody.y;
            dz = iBody.z - jBody.z;

            distance = (dx*dx + dy*dy + dz*dz).sqrt();
            e = e - (iBody.mass * jBody.mass) / distance;

            j = j + 1L;
        }

         i = i + 1L;
      }

      return e;
   }
}

const PI: Double = 3.141592653589793;
var SOLAR_MASS: Double;
const DAYS_PER_YEAR: Double = 365.24;

class Body {
    var x: Double;
    var y: Double;
    var z: Double;
    var vx: Double;
    var vy: Double;
    var vz: Double;
    var mass: Double;

    @static fun jupiter() -> Body {
        let p = Body();
        p.x = 4.84143144246472090e+00;
        p.y = -1.16032004402742839e+00;
        p.z = -1.03622044471123109e-01;
        p.vx = 1.66007664274403694e-03 * DAYS_PER_YEAR;
        p.vy = 7.69901118419740425e-03 * DAYS_PER_YEAR;
        p.vz = -6.90460016972063023e-05 * DAYS_PER_YEAR;
        p.mass = 9.54791938424326609e-04 * SOLAR_MASS;
        return p;
    }

   @static fun saturn() -> Body {
      let p = Body();
      p.x = 8.34336671824457987e+00;
      p.y = 4.12479856412430479e+00;
      p.z = -4.03523417114321381e-01;
      p.vx = -2.76742510726862411e-03 * DAYS_PER_YEAR;
      p.vy = 4.99852801234917238e-03 * DAYS_PER_YEAR;
      p.vz = 2.30417297573763929e-05 * DAYS_PER_YEAR;
      p.mass = 2.85885980666130812e-04 * SOLAR_MASS;
      return p;
   }

   @static fun uranus() -> Body {
      let p = Body();
      p.x = 1.28943695621391310e+01;
      p.y = -1.51111514016986312e+01;
      p.z = -2.23307578892655734e-01;
      p.vx = 2.96460137564761618e-03 * DAYS_PER_YEAR;
      p.vy = 2.37847173959480950e-03 * DAYS_PER_YEAR;
      p.vz = -2.96589568540237556e-05 * DAYS_PER_YEAR;
      p.mass = 4.36624404335156298e-05 * SOLAR_MASS;
      return p;
   }

   @static fun neptune() -> Body {
      let p = Body();
      p.x = 1.53796971148509165e+01;
      p.y = -2.59193146099879641e+01;
      p.z = 1.79258772950371181e-01;
      p.vx = 2.68067772490389322e-03 * DAYS_PER_YEAR;
      p.vy = 1.62824170038242295e-03 * DAYS_PER_YEAR;
      p.vz = -9.51592254519715870e-05 * DAYS_PER_YEAR;
      p.mass = 5.15138902046611451e-05 * SOLAR_MASS;
      return p;
   }

   @static fun sun() -> Body {
      let p = Body();
      p.mass = SOLAR_MASS;
      return p;
   }

   fun offsetMomentum(px: Double, py: Double, pz: Double) -> Body {
      self.vx = -px / SOLAR_MASS;
      self.vy = -py / SOLAR_MASS;
      self.vz = -pz / SOLAR_MASS;
      return self;
   }
}
