class Graph {
    var entry_block: Block;
    var exit_block: Block;

    var blocks: Vec[Block] = Vec[Block]();
    var reverse_post_order: Vec[Block] = Vec[Block]();

    var next_block_id: Int = 0;
    var next_inst_id: Int = 0;

    fun addBlock(block: Block) {
        assert(block.graph === nil && block.id == -1);

        block.graph = self;
        block.id = self.nextBlockId();
        self.blocks.push(block);
    }

    fun setEntryBlock(block: Block) {
        self.entry_block = block;
    }

    fun setExitBlock(block: Block) {
        self.exit_block = block;
    }

    fun nextInstId() -> Int {
        let id = self.next_inst_id;
        self.next_inst_id = id + 1;

        id
    }

    fun nextBlockId() -> Int {
        let id = self.next_block_id;
        self.next_block_id = id + 1;

        id
    }

    fun numberBlocks() -> Int {
        self.next_block_id
    }
}

class Block() {
    var id: Int = -1;

    var phis: Vec[Inst] = Vec[Inst]();
    var instructions: InstLinkedList = InstLinkedList();

    var graph: Graph;

    var dominator: Block = nil;
    var dominated_blocks: Vec[Block] = Vec[Block]();

    var predecessors: Vec[Block] = Vec[Block]();
    var successors: Vec[Block] = Vec[Block]();

    fun addPredecessor(block: Block) {
        self.predecessors.push(block);
        block.successors.push(block);
    }

    fun addSuccessor(block: Block) {
        self.successors.push(block);
        block.predecessors.push(block);
    }

    fun appendInst(inst: Inst) -> Inst {
        assert(inst.block === nil && inst.id == -1);

        inst.block = self;
        inst.id = self.graph.nextInstId();

        self.instructions.appendInst(inst);

        inst.registerUses();
        return inst;
    }
}

class Input {
    var idx: Int;

    var value: Inst;
    var used_by: Inst;

    var previous_use: Input;
    var next_use: Input;
}

@abstract @open class Inst {
    var id: Int = -1;
    var block: Block = nil;

    var previous: Inst = nil;
    var next: Inst = nil;

    var ty: Type = nil;

    var use_list_head: Input = nil;
    var use_list_tail: Input = nil;

    var inputs: Vec[Input] = Vec[Input]();

    fun getType() -> Type {
        return self.ty;
    }

    fun addInput(inst: Inst) {
        let input = Input();
        input.value = inst;
        input.used_by = self;
        input.idx = self.inputs.length();

        self.inputs.push(input);
    }

    fun addUse(input: Input) {
        if self.use_list_head === nil {
            input.previous_use = nil;
            input.next_use = nil;

            self.use_list_head = input;
            self.use_list_tail = input;
        } else {
            input.previous_use = self.use_list_tail;
            input.next_use = nil;

            self.use_list_tail = input;
        }
    }

    fun registerUses() {
        let inputs = self.inputs.length();
        var idx = 0;

        while idx < inputs {
            let input = self.inputs.get(idx);
            input.value.addUse(input);
            idx = idx + 1;
        }
    }

    fun hasUses() -> Bool {
        self.use_list_head !== nil
    }
}

@abstract @open class Const: Inst
class IntConst(let value: Int): Const
class LongConst(let value: Long): Const
class FloatConst(let value: Float): Const
class DoubleConst(let value: Double): Const

class UnaryInst(let op: UnOp, let opnd: Inst): Inst {
    self.addInput(opnd);
}

class BinaryInst(let op: BinOp, let lhs: Inst, let rhs: Inst): Inst {
    self.addInput(lhs);
    self.addInput(rhs);
}

class PhiInst(let inputs: Vec[PhiArg]): Inst
class PhiArg(let value: Inst, let block: Block)

class ArrayGet(let array: Inst, let index: Inst): Inst {
    self.addInput(array);
    self.addInput(index);
}

class ArraySet(let array: Inst, let index: Inst, let value: Inst): Inst {
    self.addInput(array);
    self.addInput(index);
    self.addInput(value);
}

class ArrayLength(let array: Inst): Inst {
    self.addInput(array);
}

class ParamInst(let index: Int, ty: Type): Inst {
    self.ty = ty;
}

class ReturnInst(let value: Inst): Inst {
    self.addInput(value);
}

class ReturnVoidInst: Inst

class CondInst(let cond: Inst, let true_block: Block, let false_block: Block): Inst {
    self.addInput(cond);
}

class GotoInst(let block: Block): Inst

enum BinOp {
    Add, Sub, Mul, Div, Mod,
    FAdd, FSub, FMul, FDiv,
}

enum UnOp {
    Neg, FNeg, Not,
}

class InstLinkedList {
    var first_inst: Inst;
    var last_inst: Inst;

    fun isEmpty() -> Bool {
        self.first_inst === nil
    }

    fun firstInst() -> Inst {
        self.first_inst
    }

    fun lastInst() -> Inst {
        self.last_inst
    }

    fun appendInst(inst: Inst) {
        if self.first_inst === nil {
            inst.previous = nil;
            inst.next = nil;

            self.first_inst = inst;
            self.last_inst = inst;
        } else {
            self.last_inst.next = inst;

            inst.previous = self.last_inst;
            inst.next = nil;

            self.last_inst = inst;
        }
    }
}

class InstIterator(block: Block) {
    var inst: Inst = block.instructions.firstInst();

    fun current() -> Inst { self.inst }
    fun isDone() -> Bool { self.inst === nil }
    fun next() {
        if self.inst !== nil {
            self.inst = self.inst.next;
        }
    }
}

class BackwardInstIterator(block: Block) {
    var inst: Inst = block.instructions.lastInst();

    fun current() -> Inst { self.inst }
    fun isDone() -> Bool { self.inst === nil }
    fun next() {
        if self.inst !== nil {
            self.inst = self.inst.previous;
        }
    }
}