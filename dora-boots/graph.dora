class Graph {
    var entryBlock: Option[Block] = None[Block];
    var exitBlock: Option[Block] = None[Block];

    var blocks: Vec[Option[Block]] = Vec[Option[Block]]();
    var postOrder: Vec[Block] = Vec[Block]();

    var nextBlockIdValue: Int32 = 0;
    var nextInstIdValue: Int32 = 0;

    fun addBlock(block: Block) {
        assert(block.graph.isNone() && block.id == -1);

        block.graph = Some[Graph](self);
        block.id = self.nextBlockId().value;
        self.blocks.push(Some[Block](block));
    }

    fun getBlockWithId(id: Int32): Block {
        self.blocks(id.toInt64()).unwrap()
    }

    fun setEntryBlock(block: Block) {
        self.entryBlock = Some[Block](block);
    }

    fun getEntryBlock(): Block {
        self.entryBlock.unwrap()
    }

    fun setExitBlock(block: Block) {
        self.exitBlock = Some[Block](block);
    }

    fun getExitBlock(): Block {
        self.exitBlock.unwrap()
    }

    fun nextInstId(): Int32 {
        let id = self.nextInstIdValue;
        self.nextInstIdValue = id + 1;

        id
    }

    fun nextBlockId(): BlockId {
        let id = self.nextBlockIdValue;
        self.nextBlockIdValue = id + 1;

        BlockId(id)
    }

    fun blockCount(): Int32 {
        self.nextBlockIdValue
    }

    fun setPostOrder(postOrder: Vec[Block]) {
        self.postOrder = postOrder;

        for (idx, block) in self.postOrder.enumerate() {
            block.postOrderIdx = idx.toInt32();
        }
    }

    fun rebuildDominatorTree() {
        // TODO: clear data of dominance and loop analysis
        self.buildDominatorTree();
    }

    fun buildDominatorTree() {
        buildDominatorTree(self);
    }

    fun insertionOrderIterator(): InsertionOrderIterator {
        InsertionOrderIterator(self.blocks.toArray())
    }

    fun postOrderIterator(): PostOrderIterator {
        PostOrderIterator(self.postOrder.toArray())
    }

    fun reversePostOrderIterator(): ReversePostOrderIterator {
        ReversePostOrderIterator(self.postOrder.toArray())
    }
}

class InsertionOrderIterator(let blocks: Array[Option[Block]]) {
    var idx: Int64 = 0L;

    fun hasNext(): Bool {
        self.findNextBlock();

        self.idx < self.blocks.size()
    }

    fun next(): Block {
        let block = self.blocks(self.idx).unwrap();
        self.idx = self.idx + 1L;
        block
    }

    fun findNextBlock() {
        while self.idx < self.blocks.size() {
            let block = self.blocks(self.idx);

            if block.isSome() {
                break;
            }

            self.idx = self.idx + 1L;
        }
    }
}

class PostOrderIterator(let postOrder: Array[Block]) {
    var idx: Int64 = 0L;

    fun hasNext(): Bool {
        self.idx < self.postOrder.size()
    }

    fun next(): Block {
        let block = self.postOrder(self.idx);
        self.idx = self.idx + 1L;
        block
    }
}

class ReversePostOrderIterator(let postOrder: Array[Block]) {
    var idx: Int64 = self.postOrder.size() - 1L;

    fun hasNext(): Bool {
        self.idx != -1L
    }

    fun next(): Block {
        let block = self.postOrder(self.idx);
        self.idx = self.idx - 1L;
        block
    }
}

struct BlockId(value: Int32)

class Block() {
    var id: Int32 = -1;

    var phis: InstLinkedList = InstLinkedList();
    var instructions: InstLinkedList = InstLinkedList();

    var graph: Option[Graph] = None[Graph];

    var dominator: Option[Block] = None[Block];
    var dominatedBlocks: Vec[Block] = Vec[Block]();

    var predecessors: Vec[Edge] = Vec[Edge]();
    var successors: Vec[Edge] = Vec[Edge]();

    var backwardPredecessors: Int32 = 0;
    var backwardSuccessors: Int32 = 0;

    var loopData: Option[LoopData] = None[LoopData];

    var postOrderIdx: Int32 = -1;

    fun getDominator(): Option[Block] {
        self.dominator
    }

    fun setDominator(block: Block) {
        self.dominator = Some[Block](block);
    }

    fun dominates(other: Block): Bool {
        if self == other {
            true
        } else {
            self.strictlyDominates(other)
        }
    }

    fun strictlyDominates(other: Block): Bool {
        var current = other.getDominator();

        while current.isSome() {
            if self === current.unwrap() { return true; }
            current = current.unwrap().getDominator();
        }

        false
    }

    fun getDominatedBlocks(): Vec[Block] {
        self.dominatedBlocks
    }

    fun addDominatedBlock(block: Block) {
        self.dominatedBlocks.push(block);
    }

    fun addSuccessor(block: Block): Edge {
        let edge = Edge(self, block);
        self.successors.push(edge);
        block.predecessors.push(edge);
        edge
    }

    fun appendPhi(phi: PhiInst): PhiInst {
        assert(phi.block.isNone() && phi.id == -1);

        phi.block = Some[Block](self);
        phi.id = self.graph.unwrap().nextInstId();

        self.phis.appendInst(phi);
        phi.registerUses();

        phi
    }

    fun appendInst(inst: Inst): Inst {
        assert(inst.block.isNone() && inst.id == -1);

        inst.block = Some[Block](self);
        inst.id = self.graph.unwrap().nextInstId();

        self.instructions.appendInst(inst);
        inst.registerUses();

        inst
    }

    fun addBackEdge(block: Block) {
        if self.loopData.isNone() {
            self.loopData = Some[LoopData](LoopData(self));
        }
        self.loopData.unwrap().addBackEdge(block);
    }

    fun isLoopHeader(): Bool {
        self.loopData.isSome() && self.loopData.unwrap().header === self
    }

    fun getLoopData(): Option[LoopData] {
        self.loopData
    }

    fun setLoopData(loop: LoopData) {
        self.loopData = Some[LoopData](loop);
    }

    fun clearLoopData() {
        self.loopData = None[LoopData];
    }

    fun isEntryBlock(): Bool {
        self.graph.unwrap().getEntryBlock() === self
    }

    fun isExitBlock(): Bool {
        self.graph.unwrap().getExitBlock() === self
    }

    fun instructionsIterator(): InstIterator {
        InstIterator(self)
    }

    fun reverseInstructionsIterator(): BackwardInstIterator {
        BackwardInstIterator(self)
    }

    fun phisIterator(): PhiInstIterator {
        PhiInstIterator(self)
    }

    fun firstInst(): Inst {
        self.instructions.firstInst()
    }

    fun lastInst(): Inst {
        self.instructions.lastInst()
    }

    fun removePhi(inst: PhiInst) {
        unimplemented();
    }
}

impl Hash for Block {
  fun hash(): Int32 = self.id.hash();
}

impl Equals for Block {
  fun equals(other: Block): Bool = self === other;
}

import std::BitVec;

class LoopData(let header: Block) {
    var backEdges: Vec[Block] = Vec[Block]();
    let blocks: BitVec = BitVec();

    fun getHeader(): Block {
        self.header
    }

    fun addBackEdge(block: Block) {
        self.backEdges.push(block);
    }

    fun numberBackEdges(): Int32 {
        self.backEdges.size().toInt32()
    }

    fun addBlock(block: Block) {
        self.blocks.insert(block.id.toInt64());
    }

    fun containsBlock(block: Block): Bool {
        self.blocks.contains(block.id.toInt64())
    }

    fun containsLoop(other: LoopData): Bool {
        self.containsBlock(other.header)
    }
}

class Input(var idx: Int32, var value: Inst, var usedBy: Inst) {
    var previousUse: Option[Input] = None[Input];
    var nextUse: Option[Input] = None[Input];
}

@abstract @open class Inst {
    var id: Int32 = -1;
    var block: Option[Block] = None[Block];

    var previous: Option[Inst] = None[Inst];
    var next: Option[Inst] = None[Inst];

    var ty: Type = Type::Unit;

    var useListHead: Option[Input] = None[Input];
    var useListTail: Option[Input] = None[Input];

    var inputs: Vec[Input] = Vec[Input]();

    fun getType(): Type {
        return self.ty;
    }

    fun addInput(inst: Inst) {
        let input = Input(self.inputs.size().toInt32(), inst, self);
        self.inputs.push(input);
    }

    fun addUse(input: Input) {
        assert(input.value === self);

        if self.useListHead.isNone() {
            assert(self.useListTail.isNone());
            input.previousUse = None[Input];
            input.nextUse = None[Input];

            self.useListHead = Some[Input](input);
            self.useListTail = Some[Input](input);
        } else {
            assert(self.useListHead.isSome());
            assert(self.useListTail.isSome());

            self.useListTail.unwrap().nextUse = Some[Input](input);

            input.previousUse = self.useListTail;
            input.nextUse = None[Input];

            self.useListTail = Some[Input](input);
        }
    }

    fun remove() {
        assert(!self.hasUses());
        assert(self.block.isSome());

        self.removeAsUser();
        let block = self.block.unwrap();

        if self is PhiInst {
            block.phis.removeInst(self);
        } else {
            block.instructions.removeInst(self);
        }

        self.block = None[Block];
    }

    fun registerUses() {
        for input in self.inputs {
            input.value.addUse(input);
        }
    }

    fun removeAsUser() {
        for input in self.inputs {
            removeUse(input);
        }
    }

    fun hasUses(): Bool {
        let result = self.useListHead.isSome();
        assert(result == self.useListTail.isSome());
        result
    }

    fun replaceWith(replacement: Inst) {
        while self.useListHead.isSome() {
            let input = self.useListHead.unwrap();
            input.usedBy.replaceInput(replacement, input.idx);
        }
    }

    fun replaceInput(replacement: Inst, idx: Int32) {
        let input = self.inputs(idx.toInt64());

        if input.value === replacement {
            return;
        }

        removeUse(input);
        input.value = replacement;
        replacement.addUse(input);
    }

    fun users(): Array[Inst] {
        let data = Vec[Inst]();
        var current = self.useListHead;

        while current.isSome() {
            let cur = current.unwrap();
            data.push(cur.usedBy);
            current = cur.nextUse;
        }

        data.toArray()
    }

    fun isDeadAndRemovable(): Bool {
        self.isRemovable() && !self.hasUses()
    }

    @open @abstract fun dumpName(): String;
    @open @abstract fun isTerminator(): Bool;
    @open @abstract fun isRemovable(): Bool;
}

fun removeUse(input: Input) {
    let inst = input.value;

    if input.previousUse.isSome() {
        input.previousUse.unwrap().nextUse = input.nextUse;
    } else {
        inst.useListHead = input.nextUse;
    }

    if input.nextUse.isSome() {
        input.nextUse.unwrap().previousUse = input.previousUse;
    } else {
        inst.useListTail = input.previousUse;
    }

    assert(inst.useListHead.isSome() == inst.useListTail.isSome());

    input.previousUse = None[Input];
    input.nextUse = None[Input];
}

@abstract @open class Const extends Inst

class NullConst(let value: UInt8) extends Const {
    @override fun dumpName(): String { "Const.Null" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class Int32Const(let value: Int32) extends Const {
    @override fun dumpName(): String { "Const.Int32(#${self.value})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class Int64Const(let value: Int64) extends Const {
    @override fun dumpName(): String { "Const.Int64(#${self.value})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class Float32Const(let value: Float32) extends Const {
    @override fun dumpName(): String { "Const.Float32(#${self.value})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class Float64Const(let value: Float64) extends Const {
    @override fun dumpName(): String { "Const.Float64(#${self.value})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class LoadGlobalInst(ty: Type, let global: GlobalId) extends Inst {
    self.ty = ty;

    @override fun dumpName(): String { "LoadGlobal.${self.ty}(${self.global.value})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class StoreGlobalInst(ty: Type, let global: GlobalId, let value: Inst) extends Inst {
    self.addInput(value);
    self.ty = ty;

    @override fun dumpName(): String { "StoreGlobal.${self.ty}(${self.global.value})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { false }
}


class UnaryInst(let op: UnOp, ty: Type, let opnd: Inst) extends Inst {
    self.addInput(opnd);
    self.ty = ty;

    @override fun dumpName(): String { "${unOpName(self.op)}.${self.ty}" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class BinaryInst(let op: BinOp, ty: Type, let lhs: Inst, let rhs: Inst) extends Inst {
    self.addInput(lhs);
    self.addInput(rhs);
    self.ty = ty;

    @override fun dumpName(): String { "${binOpName(self.op)}.${self.ty}" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class TestInst(let op: TestOp, ty: Type, let lhs: Inst, let rhs: Inst) extends Inst {
    self.addInput(lhs);
    self.addInput(rhs);
    self.ty = ty;

    @override fun dumpName(): String { "${self.op.name()}.${self.ty}" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class UndefInst extends Inst {
    @override fun dumpName(): String { "Undef" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class PhiInst extends Inst {
    @override fun dumpName(): String { "Phi" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class BoundsCheck(let index: Inst, let length: Inst) extends Inst {
    self.addInput(index);
    self.addInput(length);

    @override fun dumpName(): String { "BoundsCheck" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { false }
}

class ArrayGet(let array: Inst, let index: Inst) extends Inst {
    self.addInput(array);
    self.addInput(index);

    @override fun dumpName(): String { "ArrayGet" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class ArraySet(let array: Inst, let index: Inst, let value: Inst) extends Inst {
    self.addInput(array);
    self.addInput(index);
    self.addInput(value);

    @override fun dumpName(): String { "ArraySet" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { false }
}

class ArrayLength(let array: Inst) extends Inst {
    self.addInput(array);

    @override fun dumpName(): String { "ArrayLength" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class ArgInst(let index: Int32, ty: Type) extends Inst {
    self.ty = ty;

    @override fun dumpName(): String { "Arg.${self.ty}(${self.index})" }
    @override fun isTerminator(): Bool { false }
    @override fun isRemovable(): Bool { true }
}

class ReturnInst(let value: Inst, ty: Type) extends Inst {
    self.ty = ty;
    self.addInput(value);

    @override fun dumpName(): String { "Ret.${self.ty}" }
    @override fun isTerminator(): Bool { true }
    @override fun isRemovable(): Bool { false }
}

class ReturnVoidInst extends Inst {
    @override fun dumpName(): String { "Ret.Void" }
    @override fun isTerminator(): Bool { true }
    @override fun isRemovable(): Bool { false }
}

class IfInst(let cond: Inst, let true_block: Block, let false_block: Block) extends Inst {
    self.addInput(cond);

    @override fun dumpName(): String { "If(@${self.true_block.id}, @${self.false_block.id})" }
    @override fun isTerminator(): Bool { true }
    @override fun isRemovable(): Bool { false }
}

class GotoInst(let block: Block) extends Inst {
    @override fun dumpName(): String { "Goto(@${self.block.id})" }
    @override fun isTerminator(): Bool { true }
    @override fun isRemovable(): Bool { false }
}

enum BinOp {
    Add, Sub, Mul, Div, Mod, And, Or, Xor, Shl, Shr, Sar
}

fun binOpName(op: BinOp): String {
    if op == BinOp::Add {
        "Add"
    } else if op == BinOp::Sub {
        "Sub"
    } else if op == BinOp::Mul {
        "Mul"
    } else if op == BinOp::Div {
        "Div"
    } else if op == BinOp::Mod {
        "Mod"
    } else if op == BinOp::And {
        "And"
    } else if op == BinOp::Or {
        "Or"
    } else if op == BinOp::Xor {
        "Xor"
    } else if op == BinOp::Shl {
        "Shl"
    } else if op == BinOp::Shr {
        "Shr"
    } else if op == BinOp::Sar {
        "Sar"
    } else {
        unreachable[String]()
    }
}

enum TestOp {
    Equal,
    NotEqual,
    Greater,
    GreaterOrEqual,
    Less,
    LessOrEqual
}

impl TestOp {
    fun name(): String {
        if self == TestOp::Equal {
            "Equal"
        } else if self == TestOp::NotEqual {
            "NotEqual"
        } else if self == TestOp::Greater {
            "Greater"
        } else if self == TestOp::GreaterOrEqual {
            "GreaterOrEqual"
        } else if self == TestOp::Less {
            "Less"
        } else if self == TestOp::LessOrEqual {
            "LessOrEqual"
        } else {
            unreachable[String]()
        }
    }
}

enum UnOp {
    Neg, FNeg, Not,
}

fun unOpName(op: UnOp): String {
    if op == UnOp::Neg {
        "Neg"
    } else if op == UnOp::Not {
        "Not"
    } else {
        unreachable[String]()
    }
}

class InstLinkedList {
    var first: Option[Inst] = None[Inst];
    var last: Option[Inst] = None[Inst];

    fun isEmpty(): Bool {
        self.first.isNone()
    }

    fun firstInst(): Inst {
        self.first.unwrap()
    }

    fun lastInst(): Inst {
        self.last.unwrap()
    }

    fun appendInst(inst: Inst) {
        if self.first.isNone() {
            inst.previous = None[Inst];
            inst.next = None[Inst];

            self.first = Some[Inst](inst);
            self.last = Some[Inst](inst);
        } else {
            self.last.unwrap().next = Some[Inst](inst);

            inst.previous = self.last;
            inst.next = None[Inst];

            self.last = Some[Inst](inst);
        }
    }

    fun removeInst(inst: Inst) {
        if inst.previous.isSome() {
            let previous = inst.previous.unwrap();
            previous.next = inst.next;
        } else {
            self.first = inst.next;
        }

        if inst.next.isSome() {
            let next = inst.next.unwrap();
            next.previous = inst.previous;
        } else {
            self.last = inst.previous;
        }
    }
}

class InstIterator(block: Block) {
    var current: Option[Inst] = block.instructions.first;

    fun hasNext(): Bool {
        self.current.isSome()
    }

    fun next(): Inst {
        let inst = self.current.unwrap();
        self.current = inst.next;
        inst
    }
}

class PhiInstIterator(block: Block) {
    var current: Option[Inst] = block.phis.first;

    fun hasNext(): Bool {
        self.current.isSome()
    }

    fun next(): Inst {
        let inst = self.current.unwrap();
        self.current = inst.next;
        inst
    }
}

class BackwardInstIterator(block: Block) {
    var current: Option[Inst] = block.instructions.last;

    fun hasNext(): Bool {
        self.current.isSome()
    }

    fun next(): Inst {
        let inst = self.current.unwrap();
        self.current = inst.previous;
        inst
    }
}

class Edge(let from: Block, let to: Block) {
    var backward: Bool = false;

    fun markAsBackwardEdge() {
        assert(!self.backward);
        self.backward = true;

        self.from.backwardSuccessors = self.from.backwardSuccessors + 1;
        self.to.backwardPredecessors = self.to.backwardPredecessors + 1;
    }
}
