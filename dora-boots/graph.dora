class Graph {
    var entry_block: Block;
    var exit_block: Block;

    var blocks: Vec[Block] = Vec[Block]();
    var reverse_post_order: Vec[Block] = Vec[Block]();

    var next_block_id: Int = 0;
    var next_inst_id: Int = 0;

    fun add_block(block: Block) {
        block.graph = self;
        block.id = self.next_block_id();
        self.blocks.push(block);
    }

    fun set_entry_block(block: Block) {
        self.entry_block = block;
    }

    fun set_exit_block(block: Block) {
        self.exit_block = block;
    }

    fun next_inst_id() -> Int {
        let id = self.next_inst_id;
        self.next_inst_id = id + 1;

        id
    }

    fun next_block_id() -> Int {
        let id = self.next_block_id;
        self.next_block_id = id + 1;

        id
    }
}

class Block() {
    var id: Int;
    var phis: Vec[Inst] = Vec[Inst]();
    var instructions: InstLinkedList = InstLinkedList();
    var graph: Graph;
    var dominator: Block = nil;
    var dominated_blocks: Vec[Block] = Vec[Block]();
    var predecessors: Vec[Block] = Vec[Block]();
    var successors: Vec[Block] = Vec[Block]();

    fun append_inst(inst: Inst) -> Inst {
        inst.block = self;
        inst.id = self.graph.next_inst_id();

        self.instructions.append_inst(inst);
        return inst;
    }
}

class Input {
    var inst: Inst;

    var used_by: Inst;
    var idx: Int;

    var previous_use: Inst;
    var next_use: Inst;
}

@abstract @open class Inst {
    var id: Int;
    var block: Block;

    var previous: Inst;
    var next: Inst;

    var ty: Type;

    var use_list_first: Inst;
    var use_list_last: Inst;

    fun get_type() -> Type {
        return self.ty;
    }
}

@abstract @open class Const: Inst
class IntConst(let value: Int): Const
class FloatConst(let value: Float): Const
class DoubleConst(let value: Double): Const
class LongConst(let value: Long): Const

class PrologInst: Inst
class EpilogInst: Inst
class UnaryInst(let op: UnOp, let opnd: Inst): Inst
class BinaryInst(let op: BinOp, let lhs: Inst, let rhs: Inst): Inst
class ArgInst(let idx: Int): Inst

class PhiInst(let inputs: Vec[PhiArg]): Inst
class PhiArg(let value: Inst, let block: Block)

@abstract @open class Terminator: Inst

class ReturnInst(let value: Inst): Terminator
class ReturnVoidInst: Terminator
class CondInst(let cond: Inst, let true_succ: Block, let false_succ: Block): Terminator
class GotoInst(let succ: Block): Terminator

enum BinOp {
    Add, Sub, Mul, Div, Mod,
    FAdd, FSub, FMul, FDiv,
}

enum UnOp {
    Neg, FNeg, Not,
}

class InstLinkedList {
    var first_inst: Inst;
    var last_inst: Inst;

    fun is_empty() -> Bool {
        self.first_inst === nil
    }

    fun first_inst() -> Inst {
        self.first_inst
    }

    fun last_inst() -> Inst {
        self.last_inst
    }

    fun append_inst(inst: Inst) {
        if self.first_inst === nil {
            inst.previous = nil;
            inst.next = nil;

            self.first_inst = inst;
            self.last_inst = inst;
        } else {
            self.last_inst.next = inst;

            inst.previous = self.last_inst;
            inst.next = nil;

            self.last_inst = inst;
        }
    }
}

class InstIterator(block: Block) {
    var inst: Inst = block.instructions.first_inst();

    fun current() -> Inst { self.inst }
    fun is_done() -> Bool { self.inst === nil }
    fun next() {
        if self.inst !== nil {
            self.inst = self.inst.next;
        }
    }
}

class BackwardInstIterator(block: Block) {
    var inst: Inst = block.instructions.last_inst();

    fun current() -> Inst { self.inst }
    fun is_done() -> Bool { self.inst === nil }
    fun next() {
        if self.inst !== nil {
            self.inst = self.inst.previous;
        }
    }
}