class Graph {
    var entryBlock: Option[Block] = None;

    var blocks: Vec[Option[Block]] = Vec();
    var postOrder: Vec[Block] = Vec();

    var nextBlockIdValue: Int32 = 0I;
    var nextInstIdValue: Int32 = 0I;

    fn addBlock(block: Block) {
        assert(block.graph.isNone() && block.id == -1I);

        block.graph = Some(self);
        block.id = self.nextBlockId().value;
        self.blocks.push(Some[Block](block));
    }

    fn getBlockWithId(id: Int32): Block {
        self.blocks(id.toInt64()).getOrPanic()
    }

    fn setEntryBlock(block: Block) {
        self.entryBlock = Some(block);
    }

    fn getEntryBlock(): Block {
        self.entryBlock.getOrPanic()
    }

    fn nextInstId(): Int32 {
        let id = self.nextInstIdValue;
        self.nextInstIdValue = id + 1I;

        id
    }

    fn nextBlockId(): BlockId {
        let id = self.nextBlockIdValue;
        self.nextBlockIdValue = id + 1I;

        BlockId(id)
    }

    fn blockCount(): Int32 {
        self.nextBlockIdValue
    }

    fn setPostOrder(postOrder: Vec[Block]) {
        self.postOrder = postOrder;

        for (idx, block) in self.postOrder.enumerate() {
            block.postOrderIdx = idx.toInt32();
        }
    }

    fn rebuildDominatorTree() {
        // TODO: clear data of dominance and loop analysis
        self.buildDominatorTree();
    }

    fn buildDominatorTree() {
        buildDominatorTree(self);
    }

    fn insertionOrderIterator(): InsertionOrderIterator {
        InsertionOrderIterator(self.blocks.toArray())
    }

    fn postOrderIterator(): PostOrderIterator {
        PostOrderIterator(self.postOrder.toArray())
    }

    fn reversePostOrderIterator(): ReversePostOrderIterator {
        ReversePostOrderIterator(self.postOrder.toArray())
    }
}

class InsertionOrderIterator(let blocks: Array[Option[Block]]) {
    var idx: Int64 = 0L;

    fn hasNext(): Bool {
        self.findNextBlock();

        self.idx < self.blocks.size()
    }

    fn next(): Block {
        let block = self.blocks(self.idx).getOrPanic();
        self.idx = self.idx + 1L;
        block
    }

    fn findNextBlock() {
        while self.idx < self.blocks.size() {
            let block = self.blocks(self.idx);

            if block.isSome() {
                break;
            }

            self.idx = self.idx + 1L;
        }
    }
}

class PostOrderIterator(let postOrder: Array[Block]) {
    var idx: Int64 = 0L;

    fn hasNext(): Bool {
        self.idx < self.postOrder.size()
    }

    fn next(): Block {
        let block = self.postOrder(self.idx);
        self.idx = self.idx + 1L;
        block
    }
}

class ReversePostOrderIterator(let postOrder: Array[Block]) {
    var idx: Int64 = self.postOrder.size() - 1L;

    fn hasNext(): Bool {
        self.idx != -1L
    }

    fn next(): Block {
        let block = self.postOrder(self.idx);
        self.idx = self.idx - 1L;
        block
    }
}

struct BlockId(value: Int32)

class Block() {
    var id: Int32 = -1I;

    var phis: InstLinkedList = InstLinkedList();
    var instructions: InstLinkedList = InstLinkedList();

    var graph: Option[Graph] = None;

    var dominator: Option[Block] = None;
    var dominatedBlocks: Vec[Block] = Vec();

    var predecessors: Vec[Edge] = Vec();
    var successors: Vec[Edge] = Vec();

    var loopData: Option[LoopData] = None;

    var postOrderIdx: Int32 = -1I;

    fn getDominator(): Option[Block] = self.dominator;

    fn setDominator(block: Block) {
        self.dominator = Some(block);
    }

    fn dominates(other: Block): Bool =
        if self == other {
            true
        } else {
            self.strictlyDominates(other)
        };

    fn strictlyDominates(other: Block): Bool {
        var current = other.getDominator();

        while current.isSome() {
            if self === current.getOrPanic() { return true; }
            current = current.getOrPanic().getDominator();
        }

        false
    }

    fn getDominatedBlocks(): Vec[Block] = self.dominatedBlocks;

    fn addDominatedBlock(block: Block) {
        self.dominatedBlocks.push(block);
    }

    fn addSuccessor(block: Block): Edge {
        let edge = Edge(self, block);
        self.successors.push(edge);
        block.predecessors.push(edge);
        edge
    }

    fn appendPhi(phi: PhiInst): PhiInst {
        assert(phi.block.isNone() && phi.id == -1I);

        phi.block = Some(self);
        phi.id = self.graph.getOrPanic().nextInstId();

        self.phis.appendInst(phi);
        phi.registerUses();

        phi
    }

    fn appendInst(inst: Inst): Inst {
        assert(inst.block.isNone() && inst.id == -1I);

        inst.block = Some(self);
        inst.id = self.graph.getOrPanic().nextInstId();

        self.instructions.appendInst(inst);
        inst.registerUses();

        inst
    }

    fn addBackEdge(block: Block) {
        if self.loopData.isNone() {
            self.loopData = Some(LoopData(self));
        }
        self.loopData.getOrPanic().addBackEdge(block);
    }

    fn isLoopHeader(): Bool {
        self.loopData.isSome() && self.loopData.getOrPanic().header === self
    }

    fn getLoopData(): Option[LoopData] {
        self.loopData
    }

    fn setLoopData(loop: LoopData) {
        self.loopData = Some(loop);
    }

    fn clearLoopData() {
        self.loopData = None;
    }

    fn isEntryBlock(): Bool {
        self.graph.getOrPanic().getEntryBlock() === self
    }

    fn instructionsIterator(): InstIterator {
        InstIterator(self)
    }

    fn reverseInstructionsIterator(): BackwardInstIterator {
        BackwardInstIterator(self)
    }

    fn phisIterator(): PhiInstIterator {
        PhiInstIterator(self)
    }

    fn firstInst(): Inst {
        self.instructions.firstInst()
    }

    fn lastInst(): Inst {
        self.instructions.lastInst()
    }

    fn removePhi(inst: PhiInst) {
        unimplemented();
    }
}

impl Hash for Block {
  fn hash(): Int32 = self.id.hash();
}

impl Equals for Block {
  fn equals(other: Block): Bool = self === other;
}

import std::BitVec;

class LoopData(let header: Block) {
    var backEdges: Vec[Block] = Vec();
    let blocks: BitVec = BitVec();

    fn getHeader(): Block {
        self.header
    }

    fn addBackEdge(block: Block) {
        self.backEdges.push(block);
    }

    fn numberBackEdges(): Int32 {
        self.backEdges.size().toInt32()
    }

    fn addBlock(block: Block) {
        self.blocks.insert(block.id.toInt64());
    }

    fn containsBlock(block: Block): Bool {
        self.blocks.contains(block.id.toInt64())
    }

    fn containsLoop(other: LoopData): Bool {
        self.containsBlock(other.header)
    }
}

class Input(var idx: Int32, var value: Inst, var usedBy: Inst) {
    var previousUse: Option[Input] = None;
    var nextUse: Option[Input] = None;
}

@abstract @open class Inst {
    var id: Int32 = -1I;
    var block: Option[Block] = None;

    var previous: Option[Inst] = None;
    var next: Option[Inst] = None;

    var ty: Type = Type::Unit;

    var useListHead: Option[Input] = None;
    var useListTail: Option[Input] = None;

    var inputs: Vec[Input] = Vec();

    fn getType(): Type {
        return self.ty;
    }

    fn addInput(inst: Inst) {
        let input = Input(self.inputs.size().toInt32(), inst, self);
        self.inputs.push(input);
    }

    fn addUse(input: Input) {
        assert(input.value === self);

        if self.useListHead.isNone() {
            assert(self.useListTail.isNone());
            input.previousUse = None;
            input.nextUse = None;

            self.useListHead = Some(input);
            self.useListTail = Some(input);
        } else {
            assert(self.useListHead.isSome());
            assert(self.useListTail.isSome());

            self.useListTail.getOrPanic().nextUse = Some(input);

            input.previousUse = self.useListTail;
            input.nextUse = None;

            self.useListTail = Some(input);
        }
    }

    fn remove() {
        assert(!self.hasUses());
        assert(self.block.isSome());

        self.removeAsUser();
        let block = self.block.getOrPanic();

        if self is PhiInst {
            block.phis.removeInst(self);
        } else {
            block.instructions.removeInst(self);
        }

        self.block = None;
    }

    fn registerUses() {
        for input in self.inputs {
            input.value.addUse(input);
        }
    }

    fn removeAsUser() {
        for input in self.inputs {
            removeUse(input);
        }
    }

    fn hasUses(): Bool {
        let result = self.useListHead.isSome();
        assert(result == self.useListTail.isSome());
        result
    }

    fn replaceWith(replacement: Inst) {
        while self.useListHead.isSome() {
            let input = self.useListHead.getOrPanic();
            input.usedBy.replaceInput(replacement, input.idx);
        }
    }

    fn replaceInput(replacement: Inst, idx: Int32) {
        let input = self.inputs(idx.toInt64());

        if input.value === replacement {
            return;
        }

        removeUse(input);
        input.value = replacement;
        replacement.addUse(input);
    }

    fn users(): Array[Inst] {
        let data = Vec[Inst]();
        var current = self.useListHead;

        while current.isSome() {
            let cur = current.getOrPanic();
            data.push(cur.usedBy);
            current = cur.nextUse;
        }

        data.toArray()
    }

    fn uses(): UseIterator {
        UseIterator(self)
    }

    fn dominates(other: Inst): Bool {
        let sblock = self.block.getOrPanic();
        let oblock = other.block.getOrPanic();

        if sblock === oblock {
            var current = self.next;

            while current.isSome() {
                let currentInst = current.getOrPanic();

                if currentInst === other {
                    return true;
                }

                current = currentInst.next;
            }

            false
        } else {
            sblock.dominates(oblock)
        }
    }

    fn isDeadAndRemovable(): Bool {
        self.isRemovable() && !self.hasUses()
    }

    @open @abstract fn dumpName(): String;
    @open @abstract fn isTerminator(): Bool;
    @open @abstract fn isRemovable(): Bool;
}

class UseIterator(inst: Inst) {
    var current: Option[Input] = inst.useListHead;

    fn hasNext(): Bool {
        self.current.isSome()
    }

    fn next(): Input {
        let input = self.current.getOrPanic();
        self.current = input.nextUse;
        input
    }
}

fn removeUse(input: Input) {
    let inst = input.value;

    if input.previousUse.isSome() {
        input.previousUse.getOrPanic().nextUse = input.nextUse;
    } else {
        inst.useListHead = input.nextUse;
    }

    if input.nextUse.isSome() {
        input.nextUse.getOrPanic().previousUse = input.previousUse;
    } else {
        inst.useListTail = input.previousUse;
    }

    assert(inst.useListHead.isSome() == inst.useListTail.isSome());

    input.previousUse = None;
    input.nextUse = None;
}

@abstract @open class Const extends Inst

class NullConst(let value: UInt8) extends Const {
    @override fn dumpName(): String { "Const.Null" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class Int32Const(let value: Int32) extends Const {
    @override fn dumpName(): String { "Const.Int32(#${self.value})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class Int64Const(let value: Int64) extends Const {
    @override fn dumpName(): String { "Const.Int64(#${self.value})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class Float32Const(let value: Float32) extends Const {
    @override fn dumpName(): String { "Const.Float32(#${self.value})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class Float64Const(let value: Float64) extends Const {
    @override fn dumpName(): String { "Const.Float64(#${self.value})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class LoadGlobalInst(ty: Type, let global: GlobalId) extends Inst {
    self.ty = ty;

    @override fn dumpName(): String { "LoadGlobal.${self.ty}(${self.global.value})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class StoreGlobalInst(ty: Type, let global: GlobalId, let value: Inst) extends Inst {
    self.addInput(value);
    self.ty = ty;

    @override fn dumpName(): String { "StoreGlobal.${self.ty}(${self.global.value})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { false }
}


class UnaryInst(let op: UnOp, ty: Type, let opnd: Inst) extends Inst {
    self.addInput(opnd);
    self.ty = ty;

    @override fn dumpName(): String { "${unOpName(self.op)}.${self.ty}" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class BinaryInst(let op: BinOp, ty: Type, let lhs: Inst, let rhs: Inst) extends Inst {
    self.addInput(lhs);
    self.addInput(rhs);
    self.ty = ty;

    @override fn dumpName(): String { "${binOpName(self.op)}.${self.ty}" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class TestInst(let op: TestOp, ty: Type, let lhs: Inst, let rhs: Inst) extends Inst {
    self.addInput(lhs);
    self.addInput(rhs);
    self.ty = ty;

    @override fn dumpName(): String { "${self.op.name()}.${self.ty}" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class UndefInst extends Inst {
    @override fn dumpName(): String { "Undef" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class PhiInst extends Inst {
    @override fn dumpName(): String { "Phi" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class BoundsCheck(let index: Inst, let length: Inst) extends Inst {
    self.addInput(index);
    self.addInput(length);

    @override fn dumpName(): String { "BoundsCheck" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { false }
}

class DivZeroCheck(let value: Inst) extends Inst {
    self.addInput(value);

    @override fn dumpName(): String { "DivZeroCheck" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { false }
}

class ArrayGet(let array: Inst, let index: Inst, ty: Type) extends Inst {
    self.ty = ty;

    self.addInput(array);
    self.addInput(index);

    @override fn dumpName(): String { "ArrayGet.${self.ty}" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class ArraySet(let array: Inst, let index: Inst, let value: Inst) extends Inst {
    self.addInput(array);
    self.addInput(index);
    self.addInput(value);

    @override fn dumpName(): String { "ArraySet" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { false }
}

class ArrayLength(let array: Inst) extends Inst {
    self.addInput(array);

    @override fn dumpName(): String { "ArrayLength" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class FieldInfo(let offset: Int32, let volatile: Bool)

class FieldGet(let obj: Inst, let field: FieldInfo, ty: Type) extends Inst {
    self.ty = ty;

    self.addInput(obj);

    @override fn dumpName(): String { "FieldGet.${self.ty}" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class FieldSet(let obj: Inst, let field: FieldInfo, let value: Inst) extends Inst {
    self.addInput(obj);
    self.addInput(value);

    @override fn dumpName(): String { "FieldSet" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { false }
}

class ArgInst(let index: Int32, ty: Type) extends Inst {
    self.ty = ty;

    @override fn dumpName(): String { "Arg.${self.ty}(${self.index})" }
    @override fn isTerminator(): Bool { false }
    @override fn isRemovable(): Bool { true }
}

class ReturnInst(let value: Inst, ty: Type) extends Inst {
    self.ty = ty;
    self.addInput(value);

    @override fn dumpName(): String { "Ret.${self.ty}" }
    @override fn isTerminator(): Bool { true }
    @override fn isRemovable(): Bool { false }
}

class ReturnVoidInst extends Inst {
    @override fn dumpName(): String { "Ret.Void" }
    @override fn isTerminator(): Bool { true }
    @override fn isRemovable(): Bool { false }
}

class IfInst(let cond: Inst, let true_block: Block, let false_block: Block) extends Inst {
    self.addInput(cond);

    @override fn dumpName(): String { "If(@${self.true_block.id}, @${self.false_block.id})" }
    @override fn isTerminator(): Bool { true }
    @override fn isRemovable(): Bool { false }
}

class GotoInst(let block: Block) extends Inst {
    @override fn dumpName(): String { "Goto(@${self.block.id})" }
    @override fn isTerminator(): Bool { true }
    @override fn isRemovable(): Bool { false }
}

enum BinOp {
    Add, Sub, Mul, Div, Mod, And, Or, Xor, Shl, Shr, Sar
}

fn binOpName(op: BinOp): String {
    if op == BinOp::Add {
        "Add"
    } else if op == BinOp::Sub {
        "Sub"
    } else if op == BinOp::Mul {
        "Mul"
    } else if op == BinOp::Div {
        "Div"
    } else if op == BinOp::Mod {
        "Mod"
    } else if op == BinOp::And {
        "And"
    } else if op == BinOp::Or {
        "Or"
    } else if op == BinOp::Xor {
        "Xor"
    } else if op == BinOp::Shl {
        "Shl"
    } else if op == BinOp::Shr {
        "Shr"
    } else if op == BinOp::Sar {
        "Sar"
    } else {
        unreachable[String]()
    }
}

enum TestOp {
    Equal,
    NotEqual,
    Greater,
    GreaterOrEqual,
    Less,
    LessOrEqual
}

impl TestOp {
    fn name(): String {
        if self == TestOp::Equal {
            "Equal"
        } else if self == TestOp::NotEqual {
            "NotEqual"
        } else if self == TestOp::Greater {
            "Greater"
        } else if self == TestOp::GreaterOrEqual {
            "GreaterOrEqual"
        } else if self == TestOp::Less {
            "Less"
        } else if self == TestOp::LessOrEqual {
            "LessOrEqual"
        } else {
            unreachable[String]()
        }
    }
}

enum UnOp {
    Neg, FNeg, Not,
}

fn unOpName(op: UnOp): String {
    if op == UnOp::Neg {
        "Neg"
    } else if op == UnOp::Not {
        "Not"
    } else {
        unreachable[String]()
    }
}

class InstLinkedList {
    var first: Option[Inst] = None;
    var last: Option[Inst] = None;

    fn isEmpty(): Bool {
        self.first.isNone()
    }

    fn firstInst(): Inst {
        self.first.getOrPanic()
    }

    fn lastInst(): Inst {
        self.last.getOrPanic()
    }

    fn appendInst(inst: Inst) {
        if self.first.isNone() {
            inst.previous = None;
            inst.next = None;

            self.first = Some(inst);
            self.last = Some(inst);
        } else {
            self.last.getOrPanic().next = Some(inst);

            inst.previous = self.last;
            inst.next = None;

            self.last = Some(inst);
        }
    }

    fn removeInst(inst: Inst) {
        if inst.previous.isSome() {
            let previous = inst.previous.getOrPanic();
            previous.next = inst.next;
        } else {
            self.first = inst.next;
        }

        if inst.next.isSome() {
            let next = inst.next.getOrPanic();
            next.previous = inst.previous;
        } else {
            self.last = inst.previous;
        }
    }
}

class InstIterator(block: Block) {
    var current: Option[Inst] = block.instructions.first;

    fn hasNext(): Bool {
        self.current.isSome()
    }

    fn next(): Inst {
        let inst = self.current.getOrPanic();
        self.current = inst.next;
        inst
    }
}

class PhiInstIterator(block: Block) {
    var current: Option[Inst] = block.phis.first;

    fn hasNext(): Bool {
        self.current.isSome()
    }

    fn next(): PhiInst {
        let inst = self.current.getOrPanic();
        self.current = inst.next;
        inst as PhiInst
    }
}

class BackwardInstIterator(block: Block) {
    var current: Option[Inst] = block.instructions.last;

    fn hasNext(): Bool {
        self.current.isSome()
    }

    fn next(): Inst {
        let inst = self.current.getOrPanic();
        self.current = inst.previous;
        inst
    }
}

class Edge(let source: Block, let target: Block) {
    var backward: Bool = false;

    fn markAsBackwardEdge() {
        assert(!self.backward);
        self.backward = true;
    }
}
