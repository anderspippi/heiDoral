fun assertDominatedBlocks(fct: BytecodeFunction, idoms: Vec[Int]) {
    let graph = createGraph(fct);
    graph.buildDominatorTree();
    let it = idoms.makeIterator();
    var idx = 1; // Block with id 0 is entry block

    assert(graph.getEntryBlock().getDominator().isNone());

    while it.hasNext() {
        let dominatorId = it.next();
        let block = graph.getBlockWithId(idx);

        let dominator = block.getDominator();

        if dominator.isNone() {
            println("block ${idx}: missing dominator, expected ${dominatorId}");
        } else if dominator.unwrap().id != dominatorId {
            println("block ${idx}: idom is ${dominator.unwrap().id}, expected ${dominatorId}");
            println(dumpGraph(graph));
        }

        assert(dominator.unwrap().id == dominatorId);
        idx = idx + 1;
    }
}

@test fun testDominatorRetVoid(_x: Testing) {
    let writer = BytecodeWriter();
    writer.emitRetVoid();
    let fct = writer.generate();

    let idoms = Vec[Int]();
    idoms.push(2);
    idoms.push(0);

    assertDominatedBlocks(fct, idoms);
}

@test fun testDominatorEmptyBlocks(_x: Testing) {
    let writer = BytecodeWriter();
    let next = writer.createLabel();
    writer.emitJump(next);
    writer.bindLabel(next);
    let next = writer.createLabel();
    writer.emitJump(next);
    writer.bindLabel(next);
    writer.emitRetVoid();
    let fct = writer.generate();

    let idoms = Vec[Int]();
    idoms.push(4);
    idoms.push(0);
    idoms.push(2);
    idoms.push(3);

    assertDominatedBlocks(fct, idoms);
}

@test fun testDominatorSimple(_x: Testing) {
    let writer = BytecodeWriter();
    let arg0 = writer.addRegister(BytecodeType::bool());
    writer.setArguments(1);
    let elseBlock = writer.createLabel();
    writer.emitJumpIfFalse(arg0, elseBlock);
    let returnBlock = writer.createLabel();
    writer.emitJump(returnBlock);
    writer.bindLabel(elseBlock);
    writer.emitJump(returnBlock);
    writer.bindLabel(returnBlock);
    writer.emitRetVoid();
    let fct = writer.generate();

    let idoms = Vec[Int]();
    idoms.push(5);
    idoms.push(0);
    idoms.push(2);
    idoms.push(2);
    idoms.push(2);

    assertDominatedBlocks(fct, idoms);
}

@test fun testDominatorIfReturnInBothBranches(_x: Testing) {
    let fct = bytecode("bcSsaGenIfReturnInBothBranches");
    let graph = createGraph(fct);
    graph.buildDominatorTree();

    let entry = graph.getEntryBlock();
    let condBlock = graph.getBlockWithId(2);
    let thenBlock = graph.getBlockWithId(4);
    let elseBlock = graph.getBlockWithId(3);
    let exit = graph.getExitBlock();

    assert(entry.getDominatedBlocks().length() == 1);
    assert(entry.getDominator().isNone());

    assert(condBlock.getDominator().unwrap() === entry);
    assert(condBlock.getDominatedBlocks().length() == 3);

    assert(thenBlock.getDominator().unwrap() === condBlock);
    assert(thenBlock.getDominatedBlocks().length() == 0);

    assert(elseBlock.getDominator().unwrap() === condBlock);
    assert(elseBlock.getDominatedBlocks().length() == 0);

    assert(exit.getDominatedBlocks().length() == 0);
    assert(exit.getDominator().unwrap() === condBlock);
}

@test fun testDominatorWhileCountDown(_x: Testing) {
    let fct = bytecode("bcSsaGenWhileCountDown");
    let graph = createGraph(fct);
    graph.buildDominatorTree();

    let entry = graph.getEntryBlock();
    let whileHeader = graph.getBlockWithId(2);
    let whileBody = graph.getBlockWithId(4);
    let returnBlock = graph.getBlockWithId(3);
    let exit = graph.getExitBlock();

    assert(entry.getDominatedBlocks().length() == 1);
    assert(entry.getDominator().isNone());

    assert(whileHeader.getDominator().unwrap() === entry);
    assert(whileHeader.getDominatedBlocks().length() == 2);

    assert(whileBody.getDominator().unwrap() === whileHeader);
    assert(whileBody.getDominatedBlocks().length() == 0);

    assert(returnBlock.getDominator().unwrap() === whileHeader);
    assert(returnBlock.getDominatedBlocks().length() == 1);

    assert(exit.getDominatedBlocks().length() == 0);
    assert(exit.getDominator().unwrap() === returnBlock);
}
