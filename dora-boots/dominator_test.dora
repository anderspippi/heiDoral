fun assertDominatedBlocks(fct: BytecodeFunction, idoms: Int32...) {
    let graph = createGraph(fct);
    graph.buildDominatorTree();

    assert(graph.getEntryBlock().getDominator().isNone());

    for (idx, expectedDominatorId) in idoms.enumerate() {
        // Block 0 is entry block and idoms doesn't contain that block so skip it.
        let block = graph.getBlockWithId(idx.toInt32() + 1);

        let dominator = block.getDominator();

        if dominator.isNone() {
            println("block ${idx}: missing dominator, expected ${expectedDominatorId}");
        } else if dominator.unwrap().id != expectedDominatorId {
            println("block ${idx}: idom is ${dominator.unwrap().id}, expected ${expectedDominatorId}");
            println(dumpGraph(graph));
        }

        assert(dominator.unwrap().id == expectedDominatorId);
    }
}

@test fun testDominatorRetVoid(_x: Testing) {
    let writer = BytecodeWriter();
    writer.emitRetVoid();
    let fct = writer.generate();

    assertDominatedBlocks(fct, 2, 0);
}

@test fun testDominatorEmptyBlocks(_x: Testing) {
    let writer = BytecodeWriter();
    let next = writer.createLabel();
    writer.emitJump(next);
    writer.bindLabel(next);
    let next = writer.createLabel();
    writer.emitJump(next);
    writer.bindLabel(next);
    writer.emitRetVoid();
    let fct = writer.generate();

    assertDominatedBlocks(fct, 4, 0, 2, 3);
}

@test fun testDominatorSimple(_x: Testing) {
    let writer = BytecodeWriter();
    let arg0 = writer.addRegister(BytecodeType::bool());
    writer.setArguments(1);
    let elseBlock = writer.createLabel();
    writer.emitJumpIfFalse(arg0, elseBlock);
    let returnBlock = writer.createLabel();
    writer.emitJump(returnBlock);
    writer.bindLabel(elseBlock);
    writer.emitJump(returnBlock);
    writer.bindLabel(returnBlock);
    writer.emitRetVoid();
    let fct = writer.generate();

    assertDominatedBlocks(fct, 5, 0, 2, 2, 2);
}

@test fun testDominatorIfReturnInBothBranches(_x: Testing) {
    let fct = bytecode("bcSsaGenIfReturnInBothBranches");
    let graph = createGraph(fct);
    graph.buildDominatorTree();

    let entry = graph.getEntryBlock();
    let condBlock = graph.getBlockWithId(2);
    let thenBlock = graph.getBlockWithId(4);
    let elseBlock = graph.getBlockWithId(3);
    let exit = graph.getExitBlock();

    assert(entry.getDominatedBlocks().size() == 1L);
    assert(entry.getDominator().isNone());

    assert(condBlock.getDominator().unwrap() === entry);
    assert(condBlock.getDominatedBlocks().size() == 3L);

    assert(thenBlock.getDominator().unwrap() === condBlock);
    assert(thenBlock.getDominatedBlocks().size() == 0L);

    assert(elseBlock.getDominator().unwrap() === condBlock);
    assert(elseBlock.getDominatedBlocks().size() == 0L);

    assert(exit.getDominatedBlocks().size() == 0L);
    assert(exit.getDominator().unwrap() === condBlock);
}

@test fun testDominatorWhileCountDown(_x: Testing) {
    let fct = bytecode("bcSsaGenWhileCountDown");
    let graph = createGraph(fct);
    graph.buildDominatorTree();

    let entry = graph.getEntryBlock();
    let varBlock = graph.getBlockWithId(2);
    let whileHeader = graph.getBlockWithId(5);
    let whileBody = graph.getBlockWithId(4);
    let returnBlock = graph.getBlockWithId(3);
    let exit = graph.getExitBlock();

    assert(entry.getDominatedBlocks().size() == 1L);
    assert(entry.getDominator().isNone());

    assert(varBlock.getDominator().unwrap() == entry);

    assert(whileHeader.getDominator().unwrap() === varBlock);
    assert(whileHeader.getDominatedBlocks().size() == 2L);

    assert(whileBody.getDominator().unwrap() === whileHeader);
    assert(whileBody.getDominatedBlocks().size() == 0L);

    assert(returnBlock.getDominator().unwrap() === whileHeader);
    assert(returnBlock.getDominatedBlocks().size() == 1L);

    assert(exit.getDominatedBlocks().size() == 0L);
    assert(exit.getDominator().unwrap() === returnBlock);
}
