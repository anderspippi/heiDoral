@internal fun fatalError(msg: String);
@internal fun abort();
@internal fun exit(status: Int);
fun unreachable() {
  fatalError("unreachable code");
}

fun unimplemented() = fatalError("not yet implemented");

@internal fun print(text: String);
@internal fun println(text: String);
@internal fun assert(val: Bool);
@internal fun debug();
@internal fun argc() -> Int;
@internal fun argv(idx: Int) -> String;
@internal fun forceCollect();
@internal fun forceMinorCollect();

@internal fun call(fct: String);

@internal fun timestamp() -> Int64;

class Object

fun bubbleSort[T: Comparable](array: Array[T]) {
  let len = array.length();
  var swapped = true;

  while swapped {
    swapped = false;

    var i = 1;

    while i < len {
      if array.get(i-1).compareTo(array.get(i)) > 0I {
        let temp = array.get(i-1);
        array.set(i-1, array.get(i));
        array.set(i, temp);

        swapped = true;
      }

      i = i + 1;
    }
  }
}

fun isValidUtf8(data: Array[UInt8]) -> Bool {
  var i = 0L;

  while i < data.length().toInt64() {
    let by = data.get(i.toInt()).toInt32();
    var codePoint = 0I;
    var nextUInt8s = 0I;
    var min = 0I;
    var max = 0I;

    if by & 0x80I == 0I {
      nextUInt8s = 0I;
      i = i + 1L;
      continue;

    } else if by & 0xC0I == 0x80I {
      return false;

    } else if by & 0xE0I == 0xC0I {
      codePoint = by & 0x1FI;
      nextUInt8s = 1I;
      min = 0x80I;
      max = 0x7FFI;

    } else if by & 0xF0I == 0xE0I {
      codePoint = by & 0xFI;
      nextUInt8s = 2I;
      min = 0x800I;
      max = 0xFFFFI;

    } else if by & 0xF8I == 0xF0I {
      codePoint = by & 0x7I;
      nextUInt8s = 3I;
      min = 0x10000I;
      max = 0x10FFFFI;
    }

    while nextUInt8s > 0I {
      nextUInt8s = nextUInt8s - 1I;
      i = i + 1L;

      if i >= data.length().toInt64() {
        return false;
      }

      let by = data.get(i.toInt()).toInt32();

      if by & 0xC0I != 0x80I {
        return false;
      }

      codePoint = (codePoint << 6I) | (by & 0x3FI);
    }

    if codePoint < min || codePoint > max {
      return false;
    }

    i = i + 1L;
  }

  return true;
}

@internal fun defaultValue[T]() -> T;

@internal fun sleep(seconds: Int);
