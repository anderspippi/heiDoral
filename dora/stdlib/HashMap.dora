@pub class HashMap[K: Hash + Equals, V](entries: (K, V)...) {
    // BitSet.size == capacity * 2
    // [bit 0: inserted; bit 1: deleted] * capacity
    var inserted_and_deleted: BitSet = BitSet(0L);
    var keys: Array[K] = Array[K]::empty();
    var values: Array[V] = Array[V]::empty();
    var entries: Int64 = 0L;
    var cap: Int64 = 0L;
    for entry in entries {
        self.insert(entry.0, entry.1);
    }

    @pub fun insert(key: K, value: V): Option[V] {
        self.ensureCapacity(1L);
        assert(self.entries < self.cap);

        var hash = key.hash();
        var idx = hash.toInt64() & (self.cap - 1L);
        var insert_idx = None[Int64];

        while true {
            if self.isLive(idx) {
                let current_key = self.keys.get(idx);

                if current_key.hash() == hash && current_key.equals(key) {
                    let old_value = self.values.get(idx);
                    self.values.set(idx, value);
                    return Some[V](old_value);
                }

            } else if self.isDeleted(idx) {
                // There might be live entries after a deleted one.

                if insert_idx.isNone() {
                    insert_idx = Some[Int64](idx);
                }

            } else {
                let insert_idx = insert_idx.unwrapOr(idx);

                self.inserted_and_deleted.insert(2L * insert_idx);
                self.inserted_and_deleted.remove(2L * insert_idx + 1L);

                self.keys.set(insert_idx, key);
                self.values.set(insert_idx, value);

                self.entries = self.entries + 1L;
                return None[V];
            }

            idx = (idx + 1L) & (self.cap - 1L);
        }

        unreachable[Option[V]]()
    }

    @pub fun contains(key: K): Bool {
        assert(self.entries <= self.cap);

        if self.entries == 0L {
            return false;
        }

        var hash = key.hash();
        var idx = hash.toInt64() & (self.cap - 1L);

        while true {
            if self.isLive(idx) {
                let current_key = self.keys.get(idx);

                if current_key.hash() == hash && current_key.equals(key) {
                    return true;
                }
            } else if self.isDeleted(idx) {
                // There might be live entries after a deleted one.
            } else {
                break;
            }

            idx = (idx + 1L) & (self.cap - 1L);
        }

        false
    }

    @pub fun get(key: K): Option[V] {
        assert(self.entries <= self.cap);

        if self.entries == 0L {
            return Option[V]::None;
        }

        var hash = key.hash();
        var idx = hash.toInt64() & (self.cap - 1L);

        while true {
            if self.isLive(idx) {
                let current_key = self.keys.get(idx);

                if current_key.hash() == hash && current_key.equals(key) {
                    return Option[V]::Some(self.values.get(idx));
                }
            } else if self.isDeleted(idx) {
                // There might be live entries after a deleted one.
            } else {
                break;
            }

            idx = (idx + 1L) & (self.cap - 1L);
        }

        Option[V]::None
    }

    @pub fun remove(key: K): Option[V] {
        self.shrink();

        var hash = key.hash();
        var idx = hash.toInt64() & (self.cap - 1L);

        while true {
            if self.isLive(idx) {
                let current_key = self.keys.get(idx);

                if current_key.hash() == hash && current_key.equals(key) {
                    let value = self.values.get(idx);
                    self.inserted_and_deleted.insert(2L * idx + 1L);

                    unsafeKillRefs[K](self.keys, idx);
                    unsafeKillRefs[V](self.values, idx);

                    self.entries = self.entries - 1L;
                    return Option[V]::Some(value);
                }
            } else if self.isDeleted(idx) {
                // There might be live entries after a deleted one.
            } else {
                break;
            }

            idx = (idx + 1L) & (self.cap - 1L);
        }

        Option[V]::None
    }

    fun ensureCapacity(elements_to_add: Int64) {
        if self.entries + elements_to_add < self.cap {
            if self.entries <= (self.cap - (self.cap / 4L)) {
                return;
            }
        }

        var new_capacity = 4L;
        let old_capacity = self.cap;

        if old_capacity > 0L {
            new_capacity = old_capacity * 2L;
        }

        assert(self.entries + elements_to_add < new_capacity);

        self.rehash(new_capacity);
    }

    fun shrink() {
        if self.entries > (self.cap / 4L) {
            return;
        }

        let new_capacity = self.cap / 2L;

        if new_capacity < 4L {
            return;
        }

        assert(self.entries < new_capacity);

        self.rehash(new_capacity);
    }

    fun rehash(new_capacity: Int64) {
        let old_capacity = self.cap;

        let new_map = HashMap[K, V]();
        new_map.inserted_and_deleted = BitSet(2L * new_capacity);
        new_map.keys = Array[K]::unsafeNew(new_capacity);
        new_map.values = Array[V]::unsafeNew(new_capacity);
        new_map.entries = 0L;
        new_map.cap = new_capacity;

        var idx = 0L;

        while idx < old_capacity {
            if self.isLive(idx) {
                let key = self.keys.get(idx);
                let value = self.values.get(idx);

                new_map.insert(key, value);
            }

            idx = idx + 1L;
        }

        self.inserted_and_deleted = new_map.inserted_and_deleted;
        self.keys = new_map.keys;
        self.values = new_map.values;
        self.entries = new_map.entries;
        self.cap = new_capacity;
    }

    fun isLive(idx: Int64): Bool {
        return self.inserted_and_deleted.contains(2L * idx) &&
               !self.inserted_and_deleted.contains(2L * idx + 1L);
    }

    fun isDeleted(idx: Int64): Bool {
        self.inserted_and_deleted.contains(2L * idx + 1L)
    }

    @pub fun size(): Int64 {
        return self.entries;
    }

    @pub fun isEmpty(): Bool {
        return self.entries == 0L;
    }

    @pub fun capacity(): Int64 {
        return self.cap;
    }

    @pub fun makeIterator(): HashMapIter[K, V] {
        HashMapIter[K, V](self)
    }
}

@pub class HashMapIter[K: Hash + Equals, V](let map: HashMap[K, V]) {
    var idx: Int64 = 0L;

    @pub fun hasNext(): Bool {
        while self.idx < self.map.capacity() {
            if self.map.isLive(self.idx) {
                return true;
            }

            self.idx = self.idx + 1L;
        }

        false
    }

    @pub fun next(): (K, V) {
        assert(self.map.isLive(self.idx));
        let key = self.map.keys.get(self.idx);
        let value = self.map.values.get(self.idx);
        self.idx = self.idx + 1L;
        (key, value)
    }
}
