@pub enum Option[T] {
  Some(T),
  None,
}

use Option::Some;
use Option::None;

impl[T] Option[T] {
  @pub fn isNone(): Bool {
    match self {
      Some(_) => false,
      None => true,
    }
  }

  @pub fn isSome(): Bool {
    match self {
      Some(_) => true,
      None => false,
    }
  }

  @pub fn getOrPanic(): T {
    match self {
      Some(value) => value,
      None => {
        fatalError("cannot unwrap None.");
        unreachable[T]()
      }
    }
  }

  @pub fn unwrapNone() {
    match self {
      Some(_) => {
        fatalError("cannot unwrap Some.");
      },
      None => (),
    }
  }

  @pub fn unwrapOr(alt: T): T {
    match self {
      Some(value) => value,
      None => alt,
    }
  }

  @pub fn has(val: T): Bool {
    match self {
      Some(actual) => actual === val,
      None => false,
    }
  }

  @pub fn or(alt: Option[T]): Option[T] {
    if self.isSome() {
      return self;
    } else {
      return alt;
    }
  }
}

impl[T: Default] Option[T] {
  @pub fn getOrDefault(): T {
    match self {
      Some(actual) => actual,
      None => T::default(),
    }
  }
}

impl[T: Zero] Option[T] {
  @pub fn getOrZero(): T {
    match self {
      Some(actual) => actual,
      None => T::zero(),
    }
  }
}

impl[T: Equals] Option[T] {
  @pub fn contains(rhs: T): Bool {
    if self.isSome() {
      let lhs = self.getOrPanic();
      lhs.equals(rhs)
    } else {
      false
    }
  }

  @pub fn equals(rhs: Option[T]): Bool {
    if self.isSome() {
      if rhs.isSome() {
        let lhs = self.getOrPanic();
        let rhs = rhs.getOrPanic();
        lhs.equals(rhs)
      } else {
        false
      }
    } else {
      rhs.isNone()
    }
  }
}

impl[T: Stringable] Option[T] {
  @pub fn toString(): String {
    match self {
      Some(value) => "Some(${value})",
      None => "None",
    }
  }
}
