const RANDOM_MULTIPLIER: Int64 = 25214903917L;
const RANDOM_INCREMENT: Int64 = 11L;
const RANDOM_MASK: Int64 = 0xFF_FF_FF_FF_FF_FFL;

class Random(var seed: Int64) {
  self.seed = (seed ^ RANDOM_MULTIPLIER) & RANDOM_MASK;

  fun nextIntWithBound(bound: Int) -> Int {
    assert(bound > 0);
    let bound_minus_1 = bound - 1;

    if (bound & bound_minus_1) == 0 {
      let num = self.nextInt31().toInt64();
      return ((bound.toInt64() * num) >>> 31).toInt();
    }

    var mod = 0;

    while true {
      let num = self.nextInt31();
      mod = num % bound;

      if num - mod + bound_minus_1 >= 0 {
        break;
      }
    }

    return mod;
  }

  fun nextInt() -> Int {
    self.seed = (self.seed * RANDOM_MULTIPLIER + RANDOM_INCREMENT) & RANDOM_MASK;
    return (self.seed >>> 16).toInt();
  }

  fun nextInt31() -> Int {
    return self.nextInt() & 0x7F_FF_FF_FF;
  }

  fun nextInt64() -> Int64 {
    let w1 = self.nextInt();
    let w2 = self.nextInt();

    return (w1.toInt64() << 32) | w2.toInt64();
  }
}
